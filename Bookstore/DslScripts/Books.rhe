Module Bookstore
{
   Entity Book
   {
      ShortString Code { AutoCode; }
      ShortString Title;
      Integer NumberOfPages;
      Reference Author Bookstore.Person;

      ItemFilter CommonMisspelling 'book => book.Title.Contains("curiousity")';
      InvalidData CommonMisspelling 'It is not allowed to enter misspelled word "curiousity".';

      Logging;
   }

    Entity Person
    {
      ShortString Name;
    }

    Entity Comment
    {
      Reference Book { Detail; }
      LongString Text;
    }

    Entity ChildrensBook
    {
        Extends Bookstore.Book;

        Integer AgeFrom;
        Integer AgeTo;
        IntegerRange AgeFrom AgeTo; // A simple validation.
    }

    Entity ForeignBook
    {
        Extends Bookstore.Book;

        ShortString OriginalLanguage;
        Reference Translator Bookstore.Person;
    }

    Entity Topic
    {
        ShortString Name { Unique; Required; }
    }

    Entity BookTopic
    {
        Reference Book { Detail; }
        Reference Topic { Required; }

        UniqueMultiple 'Book Topic';
    }

    //EMPLOYEES MANAGEMENT

    Entity Employee 
    {
        ShortString Name;
        ShortString VatNumber { Unique; }
    }

    Entity Manager
    {
        Extends Bookstore.Employee;

        ShortString Bonuses;

        Reference Employee Bookstore.Employee;
    }

    Entity EducationRecord
    {
        DateTime RecordDate;
        ShortString Description;

        Reference Employee Bookstore.Employee;
    }

    Entity Department
    {
        ShortString Code { Unique; }
        ShortString Description { Unique; }
    }

    Entity EmployeeDepartment
    {
        Reference Employee { Detail; }
        Reference Department { Required; }

        UniqueMultiple 'Employee Department';
    }
}